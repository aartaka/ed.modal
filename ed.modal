<> (print ?:) ?:
<> (print1 ?: ?.) (?:?.)

?(?-) (Merges the reversed work list back into upright editor state)
<> (%merge () ?t) (Buffer ?t)
<> (%merge (?h ?t) ?a) (%merge ?t (?h ?a))

?(?-) (Adds a new line after cursor)
<> (%append (> ()) ?~ ?a) (%merge ?a (> ((?~) ())))
<> (%append (> (?x ?y)) ?~ ?a) (%merge ?a (?x (> ((?~) ?y))))
<> (%append (?x ?y) ?z ?a) (%append ?y ?z (?x ?a))

?(?-) (Adds a new line before cursor)
<> (%insert (> ()) ?~ ?a) (%merge ?a (> ((?~) ())))
<> (%insert (?x (> ?y)) ?~ ?a) (%merge ?a (?x (> ((?~) ?y))))
<> (%insert (?x ?y) ?z ?a) (%insert ?y ?z (?x ?a))

?(?-) (Prints the line at cursor)
<> (%print (> ()) ?a) (print1 (\n) (%merge ?a (> ())))
<> (%print (> (?. ?y)) ?a) (print1 (?.\n) (%merge ?a (> ((?.) ?y))))
<> (%print (?x ?y) ?a) (%print ?y (?x ?a))

?(?-) (Deletes the line at cursor)
<> (%delete (> ()) ?a) (%merge ?a (> ()))
<> (%delete (> (?x ?y)) ?a) (%merge ?a (> ?y))
<> (%delete (?x ?y) ?a) (%delete ?y (?x ?a))

?(?-) (Move to the previous line)
<> (%prev (?x (> ?y)) ?a) (%merge ?a (> (?x ?y)))
<> (%prev (> ?x) ?a) (%merge ?a (> ?x))
<> (%prev (?x ?y) ?a) (%prev ?y (?x ?a))

?(?-) (Move to the next line)
<> (%next (> ()) ?a) (%merge ?a (> ()))
<> (%next (> (?x ?y)) ?a) (%merge ?a (?x (> ?y)))
<> (%next (?x ?y) ?a) (%next ?y (?x ?a))

<> (%%join ?. () ?a ?t) (%merge ?a (> ((?.) ?t)))
<> (%%join ?. (?x ?y) ?a ?t) (%%join (?. ?x) ?y ?a ?t)
?(?-) (Join the current line with the next one)
<> (%join (> ()) ?a) (%merge ?a (> ()))
<> (%join (> (?. (?* ?z))) ?a) (%%join (?.) (?*) ?a ?z)
<> (%join (?x ?y) ?a) (%join ?y (?x ?a))

?(?-) (Swap the line under cursor with the one following it)
<> (%exchange (> ()) ?a) (%merge ?a (> ()))
<> (%exchange (> (?x ())) ?a) (%merge ?a (> (?x ())))
<> (%exchange (> (?x (?y ?z))) ?a) (%merge ?a (> (?y (?x ?z))))
<> (%exchange (?x ?y) ?a) (%exchange ?y (?x ?a))

?(?-) (Print current line number (in unary))
<> (%count (> ()) ?a ?^) (print1 (?^ \n) (%merge ?a (> ())))
<> (%count (> (?x ?y)) ?a ?^) (print1 (?^ \n) (%merge ?a (> (?x ?y))))
<> (%count (?x ?y) ?a ?.) (%count ?y (?x ?a) (i ?.))

?(?-) (Scroll to N-th line (in unary, removes all previous cursors))
<> (%scroll () ?a ()) (%merge ?a ())
<> (%scroll () ?a ?n) (%merge ?a (> ()))
<> (%scroll (> ?x) ?a ?n) (%scroll ?x ?a ?n)
<> (%scroll (?x ?y) ?a ()) (%scroll ?y (?x ?a) ())
<> (%scroll (?x ?y) ?a (i ())) (%scroll ?y (?x (> ?a)) ())
<> (%scroll (?x ?y) ?a (?i ?n)) (%scroll ?y (?x ?a) ?n)

?(?-) (Editor command dispatching)
<> (editor (Buffer ?x) (q)) (print bye!\n)
<> (editor (Buffer ?x) (a)) (editor (%append ?x dummy ()) input)
<> (editor (Buffer ?x) (i)) (editor (%insert ?x dummy ()) input)
<> (editor (Buffer ?x) (p)) (editor (%print ?x ()) input)
<> (editor (Buffer ?x) (d)) (editor (%delete ?x ()) input)
<> (editor (Buffer ?x) (-)) (editor (%prev ?x ()) input)
<> (editor (Buffer ?x) (+)) (editor (%next ?x ()) input)
<> (editor (Buffer ?x) (j)) (editor (%join ?x ()) input)
<> (editor (Buffer ?x) (e)) (editor (%exchange ?x ()) input)
<> (editor (Buffer ?x) (=)) (editor (%count ?x () (i)) input)
<> (editor (Buffer ?x) (. ?*)) (editor (%scroll ?x () (?*)) input)
<> (editor (Buffer ?x) ?~) (editor (Buffer ?x) (?~))
editor (Buffer (> ())) input
