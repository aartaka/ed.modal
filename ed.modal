<> (print ?:) ?:
<> (print1 ?: ?.) (?: ?.)

?(?-) (Merges the reversed work list back into upright editor state)
<> (%merge () ?t) (editor ?t input)
<> (%merge (?h ?t) ?a) (%merge ?t (?h ?a))

?(?-) (Adds a new line after cursor)
<> (%append (> ()) ?~ ?a) (%merge ?a (> ((?~) ())))
<> (%append (> (?x ?y)) ?~ ?a) (%merge ?a (?x (> ((?~) ?y))))
<> (%append (?x ?y) ?z ?a) (%append ?y ?z (?x ?a))

?(?-) (Adds a new line after cursor)
<> (%insert (> ()) ?~ ?a) (%merge ?a (> ((?~) ())))
<> (%insert (?x (> ?y)) ?~ ?a) (%merge ?a (?x (> ((?~) ?y))))
<> (%insert (?x ?y) ?z ?a) (%insert ?y ?z (?x ?a))

?(?-) (Prints the line at cursor)
<> (%print (> ()) ?a) (print1 (\n) (%merge ?a (> (?~ ()))))
<> (%print (> (?. ?y)) ?a) (print1 (?. \n) (%merge ?a (> (?x ?y))))
<> (%print (?x ?y) ?a) (%print ?y (?x ?a))

?(?-) (Deletes the line at cursor)
<> (%delete (> ()) ?a) (%merge ?a (> ()))
<> (%delete (> (?x ?y)) ?a) (%merge ?a (> ?y))
<> (%delete (?x ?y) ?a) (%delete ?y (?x ?a))

?(?-) (Move to the previous line)
<> (%prev (?x (> ?y)) ?a) (%merge ?a (> (?x ?y)))
<> (%prev (> ?x) ?a) (%merge ?a (> ?x))
<> (%prev (?x ?y) ?a) (%prev ?y (?x ?a))

?(?-) (Move to the next line)
<> (%next (> ()) ?a) (%merge ?a (> ()))
<> (%next (> (?x ?y)) ?a) (%merge ?a (?x (> ?y)))
<> (%next (?x ?y) ?a) (%next ?y (?x ?a))

?(?-) (Editor command dispatching)
<> (editor ?x q) (print bye!\n)
<> (editor ?x a) (%append ?x dummy ())
<> (editor ?x i) (%insert ?x dummy ())
<> (editor ?x p) (%print ?x ())
<> (editor ?x d) (%delete ?x ())
<> (editor ?x -) (%prev ?x ())
<> (editor ?x +) (%next ?x ())
<> (editor ?x ?~) (editor ?x ?~)
editor (> ()) input
